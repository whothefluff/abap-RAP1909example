managed implementation in class zbp_i_r54_orders unique;

define behavior for ZI_R54_Orders alias Orders
  persistent table zr54order
  lock master
  authorization master( instance )
  etag ModifiedAt
  {
    field( mandatory ) Status;
    field( readonly ) Id, CreatedAt, ModifiedAt, CreatedBy, ModifiedBy;

    create;
    update;
    delete;

    association _Items { create; }

    validation newStatusValid on save { field Status; }

    determination assignExternalNumber on save { create; }

    action( features: instance ) Approve result [1]$self;

    mapping for zov4order corresponding;

  }

define behavior for ZI_R54_OrderItems alias Items
  persistent table zr54orderitem
  lock dependent ( UpId = Id )
  authorization dependent( UpId = Id )
  etag ModifiedAt
  {
    field( mandatory ) Quantity, Unit;
    field( readonly ) UpId, Id, CreatedAt, ModifiedAt, CreatedBy, ModifiedBy;

    update;
    delete;

    association _Header;

    validation validateNotEmpty on save { field Unit, Quantity; }

    mapping for zr54orderitem corresponding;

  }